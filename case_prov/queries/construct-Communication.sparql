# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

# Extended documentation of the WHERE clause in this query is in
# construct-wasInformedBy.sparql.

CONSTRUCT
{
  ?nUsingAction
    prov:qualifiedCommunication [
      a prov:Communication ;
      prov:activity ?nMostRecentAction ;
    ] ;
    .
}
WHERE {
  SELECT DISTINCT ?nUsingAction ?nMostRecentAction
  WHERE {
    ?nGeneratingAction
      a case-investigation:InvestigativeAction ;
      uco-action:result ?nGenerationProvenanceRecord ;
      uco-action:result ?nResult ;
      .

    {
      SELECT DISTINCT ?nMostRecentAction ?nUsingAction ?nMostRecentProvenanceRecord
      WHERE {
        ?nMostRecentAction
          a case-investigation:InvestigativeAction ;
          uco-action:result ?nMostRecentProvenanceRecord ;
          .

        ?nUsingAction
          a case-investigation:InvestigativeAction ;
          uco-action:object ?nMostRecentProvenanceRecord ;
          .

        ?nMostRecentProvenanceRecord
          a case-investigation:ProvenanceRecord ;
          .
      }
    }

    ?nUsingAction
      uco-action:object ?nResult ;
      .

    ?nGenerationProvenanceRecord
      a case-investigation:ProvenanceRecord ;
      uco-core:object ?nResult ;
      .

    ?nMostRecentProvenanceRecord
      uco-core:object ?nResult ;
      .
  }
}
