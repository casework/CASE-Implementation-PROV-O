# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

CONSTRUCT
{
  # Designate InvestigativeAction performers as Agents.
  ?nInvestigativeAction prov:wasAssociatedWith ?nAgent .
}
WHERE {
  ?nInvestigativeAction
    a case-investigation:InvestigativeAction ;
    uco-core:hasFacet ?nActionReferences ;
    .

  ?nActionReferences
    a uco-action:ActionReferencesFacet ;
    .

  {
    SELECT ?nActionReferences ?nAgent
    WHERE {
      ?nActionReferences
        uco-action:instrument ?nAgent ;
        .
      FILTER NOT EXISTS {
        ?nActionReferences
          uco-action:performer ?nPerformer ;
          .
      }
    }
  }
  UNION
  {
    SELECT ?nActionReferences ?nAgent
    WHERE {
      ?nActionReferences
        uco-action:performer ?nAgent ;
        .
      FILTER NOT EXISTS {
        ?nActionReferences
          uco-action:instrument ?nInstrument ;
      }
    }
  }
  UNION
  {
    SELECT ?nActionReferences ?nAgent
    WHERE {
      ?nActionReferences
        uco-action:performer ?nPerformer ;
        uco-action:instrument ?nAgent ;
        .
    }
  }
}
