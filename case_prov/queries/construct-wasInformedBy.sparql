# This software was developed at the National Institute of Standards
# and Technology by employees of the Federal Government in the course
# of their official duties. Pursuant to title 17 Section 105 of the
# United States Code this software is not subject to copyright
# protection and is in the public domain. NIST assumes no
# responsibility whatsoever for its use by other parties, and makes
# no guarantees, expressed or implied, about its quality,
# reliability, or any other characteristic.
#
# We would appreciate acknowledgement if the software is used.

CONSTRUCT
{
  # Identify all actions chained together by passing some intermediary
  # result, A -> result -> B.  Note that wasInformedBy is reversed.
  ?nActionB prov:wasInformedBy ?nActionA .

  #TODO - There might be a need to implement a version of this query
  # that requires provenance records be included to define a
  # wasInformedBy edge.
}
WHERE {
  # The nested SELECT DISTINCT clause removes repetitions from multiple
  # nResult nodes passed between activities.  The nResult nodes are not
  # included in the constructed results, so there's no sense making the
  # results consumer loop through the same action pair N times.
  SELECT DISTINCT ?nActionA ?nActionB
  WHERE {
    ?nActionA
      a case-investigation:InvestigativeAction ;
      uco-core:hasFacet/uco-action:result ?nResult ;
      .
    ?nActionB
      a case-investigation:InvestigativeAction ;
      uco-core:hasFacet/uco-action:object ?nResult ;
      .
  }
}
